
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.РасходнаяНакладная") Тогда
		// Заполнение шапки
		Контрагент = ДанныеЗаполнения.Контрагент;
		Ответственный = ДанныеЗаполнения.Ответственный;
		ДокументОснование = ДанныеЗаполнения.Ссылка; 
		
		Для Каждого ТекСтрокаТовары Из ДанныеЗаполнения.Товары Цикл 
			
			Если РаботаСоСправочниками.СтатусМатериала(ТекСтрокаТовары.Номенклатура) = Перечисления.СтатусыГотовностиПродукции.ГотовКОтгрузке Тогда
				НоваяСтрока = Товары.Добавить();
				НоваяСтрока.Количество = ТекСтрокаТовары.Количество;
				НоваяСтрока.Номенклатура = ТекСтрокаТовары.Номенклатура;
				НоваяСтрока.Сумма = ТекСтрокаТовары.Сумма;
				НоваяСтрока.ЦенаПродажи = ТекСтрокаТовары.ЦенаПродажи;
				НоваяСтрока.Действие = РаботаСоСправочниками.СтатусМатериала(ТекСтрокаТовары.Номенклатура);
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)

	Движения.ОстаткиМатериалов.Записывать = Истина;
	Движения.СтоимостьМатериалов.Записывать = Истина;
	Движения.Продано.Записывать = Истина;  
	Движения.ПотребностьМатериалов.Записывать = Истина;
	Движения.СтоимостьПродажи.Записывать = Истина;
	Движения.СтатусПродукции.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из Товары Цикл  
		
		Движение = Движения.СтатусПродукции.Добавить();
		Движение.Период = ТекущаяДата();
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Статус = ТекСтрокаТовары.Действие;
				
		Движение = Движения.ОстаткиМатериалов.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Наименование = ТекСтрокаТовары.Номенклатура;
		Движение.Остаток = ТекСтрокаТовары.Количество;

		Движение = Движения.СтоимостьМатериалов.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Материал = ТекСтрокаТовары.Номенклатура;
		Движение.Стоимость = ТекСтрокаТовары.Количество * РаботаСоСправочниками.ЦенаМатериалов(Дата, ТекСтрокаТовары.Номенклатура);

		Движение = Движения.Продано.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = ТекСтрокаТовары.Количество; 
		
		// регистр ПотребностьМатериалов 
		Движение = Движения.ПотребностьМатериалов.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.Наименование = ТекСтрокаТовары.Номенклатура;
		Движение.Потребность = ТекСтрокаТовары.Количество;   
		
		// регистр СтоимостьПродажи
		Движение = Движения.СтоимостьПродажи.Добавить();
		Движение.Период = Дата;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = ТекСтрокаТовары.Количество;
		Движение.Стоимость = ТекСтрокаТовары.Сумма;
	КонецЦикла;

	//Контроль остатков
	Движения.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОстаткиМатериаловОстатки.Наименование КАК Наименование,
		|	-ОстаткиМатериаловОстатки.ОстатокОстаток КАК Количество
		|ИЗ
		|	РегистрНакопления.ОстаткиМатериалов.Остатки(
		|			,
		|			Наименование В
		|				(ВЫБРАТЬ
		|					ДокументПродажиТовары.Номенклатура КАК Номенклатура
		|				ИЗ
		|					Документ.ДокументПродажи.Товары КАК ДокументПродажиТовары
		|				ГДЕ
		|					ДокументПродажиТовары.Ссылка = &Ссылка)) КАК ОстаткиМатериаловОстатки
		|ГДЕ
		|	ОстаткиМатериаловОстатки.ОстатокОстаток < 0";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда 
		Отказ = Истина;
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Сообщить("На складе недостаточно " + ВыборкаДетальныеЗаписи.Наименование + " в количестве " + ВыборкаДетальныеЗаписи.Количество);
			
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры
